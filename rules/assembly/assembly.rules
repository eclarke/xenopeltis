# -*- mode: Snakemake -*-
#
# Contig building and other assembly rules
#
# Requires IDBA_UD and CAP3.

rule merge_fq:
    input:
        r1 = str(QC_FP/'decontam'/'{sample}_R1.fastq'),
        r2 = str(QC_FP/'decontam'/'{sample}_R2.fastq')
    output:
        str(ASSEMBLY_FP/'merged_reads'/'{sample}_merged.fasta')
    shell: "fq2fa --merge --filter {input.r1} {input.r2} {output}"


rule build_contigs:
    input:
        str(ASSEMBLY_FP/'merged_reads'/'{sample}_merged.fasta')
    output:
        str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa')
    log:
        str(ASSEMBLY_FP/'log'/'idba_ud'/'{sample}.out')
    params:
        out_fp = str(ASSEMBLY_FP/'{sample}_assembly')
    threads:
        Cfg['assembly']['threads']
    shell:
        """
        idba_ud -l {input} -o {params.out_fp} \
        --num_threads {threads} --pre_correction &> {log} || \
        if [ ! -a {output} ]; then cp {params.out_fp}/contig-100.fa {output}; fi
        """

rule cap3:
    input:
        contig = str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa'),
        cap3 = str(Cfg['assembly']['cap3_fp']/'cap3')
    output:
        str(ASSEMBLY_FP/'{sample}_assembly'/'cap3-contigs.fa')
    log:
        str(ASSEMBLY_FP/'log'/'cap3'/'{sample}.out')
    shell:
        """
        {input.cap3} {input.contig} &> {log} && \
        cat {input.contig}.cap.singlets {input.contig}.cap.contigs > {output}
        """

rule minimo:
    input:
        str(ASSEMBLY_FP/'{sample}_assembly'/'contig.fa')
    output:
        block = temp(str(ASSEMBLY_FP/'{sample}_assembly'/'contig-contigs.fa')),
        unblock = str(ASSEMBLY_FP/'{sample}_assembly'/'minimo-contigs.fa')
    log:
        str(ASSEMBLY_FP/'log'/'minimo'/'{sample}.out')
    shell:
        """
        Minimo {input} -D FASTA_EXP=1 &> {log} && \
        perl local/bin/unblock_fasta.pl {output.block} > {output.unblock}
        """

rule _assembly_all:
    """Build contigs for all samples."""
    input:
        expand(
            str(ASSEMBLY_FP/'{sample}_assembly'/'cap3-contigs.fa'),
            sample=Samples.keys())